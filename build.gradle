buildscript {
    ext {
        repoConfig = {
            mavenCentral()

            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }

        jooqVersion = "3.10.4"
        junitVersion = "5.2.0"
    }

    repositories repoConfig

    dependencies {
        classpath "org.unbroken-dome.gradle-plugins:gradle-xjc-plugin:1.1.3"
        classpath "pl.allegro.tech.build:axion-release-plugin:1.9.1"
        classpath "com.bmuschko:gradle-nexus-plugin:2.3.1"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    }
}

apply plugin: "java"
apply plugin: "org.unbroken-dome.xjc"
apply plugin: "pl.allegro.tech.build.axion-release"
apply plugin: "jacoco"
apply plugin: "com.bmuschko.nexus"
apply plugin: "io.codearte.nexus-staging"

repositories repoConfig

scmVersion {
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, c -> /verjo:$v/}, replacement: {v, c -> "verjo:$v"}]
        pre 'commit'
    }
}

group "com.github.piorkowskiprzemyslaw"
version scmVersion.version
sourceCompatibility = 1.8

dependencies {
    compile "org.jooq:jooq-codegen:$jooqVersion"
    compile "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:1.11.1"

    compileOnly 'org.projectlombok:lombok:1.16.20'

    xjcClasspath "org.jvnet.jaxb2_commons:jaxb2-basics:1.11.1"
    xjcClasspath "org.jvnet.jaxb2_commons:jaxb2-basics-annotate:1.0.4"
    xjcClasspath "org.jvnet.jaxb2_commons:jaxb2-fluent-api:3.0"
    xjcClasspath "org.jvnet.jaxb2_commons:jaxb2-commons-lang:2.4"

    testCompile "com.google.jimfs:jimfs:1.1"
    testCompile "org.assertj:assertj-core:3.10.0"
    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml {
            enabled true
        }
        html {
            enabled false
        }
    }
}

wrapper {
    gradleVersion = "4.7"
    distributionType = Wrapper.DistributionType.ALL
}

xjcGenerate {
    source = fileTree('src/main/resources/schema/2_3') { include 'schema.xsd' }
    bindingFiles = fileTree('src/main/resources/schema/2_3/bindings') { include 'binding.xjb' }
    outputDirectory = file("gen/java")
    targetPackage = "com.github.piorkowskiprzemyslaw.verjo.xsd"
    extraArgs = ['-Xannotate',
                 '-Xfluent-api',
                 '-XtoString',
                 '-Xequals',
                 '-XhashCode']
}

clean {
    delete "gen"
}

modifyPom {
    project {
        name 'VerJo'
        description 'VerJo is an jOOQ plugin providing code generation from Vertabelo XML files.'
        url 'https://github.com/piorkowskiprzemyslaw/verjo'
        inceptionYear '2018'

        scm {
            url 'https://github.com/piorkowskiprzemyslaw/verjo'
            connection 'scm:https://github.com/piorkowskiprzemyslaw/verjo.git'
            developerConnection 'scm:git@github.com:piorkowskiprzemyslaw/verjo.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://raw.githubusercontent.com/piorkowskiprzemyslaw/verjo/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'piorkowskiprzemyslaw'
                name 'Przemysław Piórkowski'
                email 'piorkowskiprzemyslaw@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}